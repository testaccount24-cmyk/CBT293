* ----------------------------------------------------------------- *
*       CKIEBGEN   -   ORIGINAL PROGRAM WAS BY BALDOMERO CASTILLA   *
*                                                                   *
*                      "PRETTIFIED BY SAM GOLOB"                    *
*                      "IMPROVED BY WARREN WHITFORD"                *
*                  LEVEL - 003                                      *
* ----------------------------------------------------------------- *
CKIEBGEN CSECT
         YREGS
         USING *,R15
         B     EYECATCH
         DC    C'--- '            MAKE EYECATCHER VISIBLE
         DC    C'CKIEBGEN - GENERAL QSAM COPY PGM   - '
         DC    C'&SYSDATE',C'  ',C'&SYSTIME ',C'--- '
         DC    C'LEVEL 002 '
         DC    C'--- '            MAKE EYECATCHER VISIBLE
SAVEAREA DC    18F'0'
EYECATCH DS    0H             ENSURE ALIGNMENT
         SAVE  (14,12)
         LR    R5,R1          SAVE PARM POINTER
         LA    R1,SAVEAREA          NEW SAVE-AREA ADDR
         ST    R1,8(,R13)           FORWARD SAVE-AREA CHAIN PTR
         ST    R13,4(,R1)           BACKWARD SAVE-AREA CHAIN PTR
         LR    R13,R1               NEW SAVE AREA ADDRESS
         DROP  R15
* -----                       SET UP TWO BASE REGISTERS
         USING CKIEBGEN,R12,R11
         LR    R12,R15
         LR    R11,R12
         A     R11,=F'4096'
         LR    R1,R5          RESTORE PARM POINTER
         PRINT GEN
         OPEN  (SYSUT1,(INPUT))
         OPEN  (SYSUT2,(OUTPUT))
         OPEN  (SYSPRINT,OUTPUT)
         ZAP   INRCDS,=P'0'
         ZAP   OUTRCDS,=P'0'
LEER     GET   SYSUT1,AREA
         AP    INRCDS,=P'1'
         PUT   SYSUT2,AREA
         AP    OUTRCDS,=P'1'
         B     LEER
FIN      MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE(133),HEADER
         PUT   SYSPRINT,OUTLINE
         RDJFCB SYSUT1
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE,MSGDSN
         MVI   OUTLINE+11,C'1'            SYSUT1
         MVC   OUTLINE+19(44),JFCBDSNM    DSNAME
         PUT   SYSPRINT,OUTLINE
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE,MSGMBR
         MVC   OUTLINE+19(8),JFCBELNM     MEMBER NAME
         PUT   SYSPRINT,OUTLINE
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE,MSGDSORG          DSORG
         MVC   OUTLINE+19(3),=C'IS '
         CLI   JFCDSRG1,JFCORGIS
         BE    PUTDSORG
         MVC   OUTLINE+19(3),=C'ISU'
         CLI   JFCDSRG1,JFCORGIS+JFCORGU
         BE    PUTDSORG
         MVC   OUTLINE+19(3),=C'PS '
         CLI   JFCDSRG1,JFCORGPS
         BE    PUTDSORG
         MVC   OUTLINE+19(3),=C'PSU'
         CLI   JFCDSRG1,JFCORGPS+JFCORGU
         BE    PUTDSORG
         MVC   OUTLINE+19(3),=C'DA '
         CLI   JFCDSRG1,JFCORGDA
         BE    PUTDSORG
         MVC   OUTLINE+19(3),=C'DAU'
         CLI   JFCDSRG1,JFCORGDA+JFCORGU
         BE    PUTDSORG
         MVC   OUTLINE+19(3),=C'PO '
         CLI   JFCDSRG1,JFCORGPO
         BE    PUTDSORG
         MVC   OUTLINE+19(3),=C'POU'
         CLI   JFCDSRG1,JFCORGPO+JFCORGU
         BNE   BLKSIZE1
PUTDSORG PUT   SYSPRINT,OUTLINE
BLKSIZE1 MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE,MSGBLKSZ
         MVC   OUTLINE+19(12),EDPAT       BLKSIZE
         SR    R4,R4
         LH    R4,JFCBLKSI
         CVD   R4,DECVALUE
         ED    OUTLINE+19(12),DECVALUE+3
         PUT   SYSPRINT,OUTLINE
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE,MSGLRECL
         MVC   OUTLINE+19(12),EDPAT       LRECL
         SR    R4,R4
         LH    R4,JFCLRECL
         CVD   R4,DECVALUE
         ED    OUTLINE+19(12),DECVALUE+3
         PUT   SYSPRINT,OUTLINE
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE,MSGRECFM           RECFM
         LA    R4,OUTLINE+19
         TM    JFCRECFM,JFCUND
         BNO   NOTUND
         MVI   0(R4),C'U'
         A     R4,ONE
         B     CHKB
NOTUND   TM    JFCRECFM,JFCFIX
         BNO   NOTFIX
         MVI   0(R4),C'F'
         A     R4,ONE
NOTFIX   TM    JFCRECFM,JFCVAR
         BNO   CHKB
         MVI   0(R4),C'V'
         A     R4,ONE
CHKB     TM    JFCRECFM,JFCRFB
         BNO   NOTBLK
         MVI   0(R4),C'B'
         A     R4,ONE
NOTBLK   TM    JFCRECFM,JFCRFS
         BNO   NOTSCN
         MVI   0(R4),C'S'
         A     R4,ONE
NOTSCN   TM    JFCRECFM,JFCASA
         BNO   NOTASA
         MVI   0(R4),C'A'
         A     R4,ONE
NOTASA   TM    JFCRECFM,JFCMAC
         BNO   PUTRCFM1
         MVI   0(R4),C'M'
PUTRCFM1 PUT   SYSPRINT,OUTLINE
         RDJFCB SYSUT2
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE,MSGDSN
         MVI   OUTLINE+11,C'2'            SYSUT2
         MVC   OUTLINE+19(44),JFCBDSNM    DSNAME
         PUT   SYSPRINT,OUTLINE
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE,MSGMBR
         MVC   OUTLINE+19(8),JFCBELNM     MEMBER NAME
         PUT   SYSPRINT,OUTLINE
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE,MSGDSORG          DSORG
         MVC   OUTLINE+19(3),=C'IS '
         CLI   JFCDSRG1,JFCORGIS
         BE    PUTDSRG2
         MVC   OUTLINE+19(3),=C'ISU'
         CLI   JFCDSRG1,JFCORGIS+JFCORGU
         BE    PUTDSRG2
         MVC   OUTLINE+19(3),=C'PS '
         CLI   JFCDSRG1,JFCORGPS
         BE    PUTDSRG2
         MVC   OUTLINE+19(3),=C'PSU'
         CLI   JFCDSRG1,JFCORGPS+JFCORGU
         BE    PUTDSRG2
         MVC   OUTLINE+19(3),=C'DA '
         CLI   JFCDSRG1,JFCORGDA
         BE    PUTDSRG2
         MVC   OUTLINE+19(3),=C'DAU'
         CLI   JFCDSRG1,JFCORGDA+JFCORGU
         BE    PUTDSRG2
         MVC   OUTLINE+19(3),=C'PO '
         CLI   JFCDSRG1,JFCORGPO
         BE    PUTDSRG2
         MVC   OUTLINE+19(3),=C'POU'
         CLI   JFCDSRG1,JFCORGPO+JFCORGU
         BNE   BLKSIZE2
PUTDSRG2 PUT   SYSPRINT,OUTLINE
BLKSIZE2 MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE,MSGBLKSZ
         MVC   OUTLINE+19(12),EDPAT       BLKSIZE
         SR    R4,R4
         LH    R4,JFCBLKSI
         CVD   R4,DECVALUE
         ED    OUTLINE+19(12),DECVALUE+3
         PUT   SYSPRINT,OUTLINE
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE,MSGLRECL
         MVC   OUTLINE+19(12),EDPAT       LRECL
         SR    R4,R4
         LH    R4,JFCLRECL
         CVD   R4,DECVALUE
         ED    OUTLINE+19(12),DECVALUE+3
         PUT   SYSPRINT,OUTLINE
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE,MSGRECFM           RECFM
         LA    R4,OUTLINE+19
         TM    JFCRECFM,JFCUND
         BNO   NOTUND2
         MVI   0(R4),C'U'
         A     R4,ONE
         B     CHKB2
NOTUND2  TM    JFCRECFM,JFCFIX
         BNO   NOTFIX2
         MVI   0(R4),C'F'
         A     R4,ONE
NOTFIX2  TM    JFCRECFM,JFCVAR
         BNO   CHKB2
         MVI   0(R4),C'V'
         A     R4,ONE
CHKB2    TM    JFCRECFM,JFCRFB
         BNO   NOTBLK2
         MVI   0(R4),C'B'
         A     R4,ONE
NOTBLK2  TM    JFCRECFM,JFCRFS
         BNO   NOTSCN2
         MVI   0(R4),C'S'
         A     R4,ONE
NOTSCN2  TM    JFCRECFM,JFCASA
         BNO   NOTASA2
         MVI   0(R4),C'A'
         A     R4,ONE
NOTASA2  TM    JFCRECFM,JFCMAC
         BNO   PUTRCFM2
         MVI   0(R4),C'M'
PUTRCFM2 PUT   SYSPRINT,OUTLINE
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE(133),MSG01
         MVC   OUTLINE+19(12),EDPAT
         ED    OUTLINE+19(12),INRCDS+2
         PUT   SYSPRINT,OUTLINE
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE(133),MSG02
         MVC   OUTLINE+19(12),EDPAT
         ED    OUTLINE+19(12),OUTRCDS+2
         PUT   SYSPRINT,OUTLINE
         CLOSE SYSUT1
         CLOSE SYSUT2
         CLOSE SYSPRINT
THEEND   DS    0H
         L     R13,4(,R13)
         RETURN (14,12),RC=0
* ----- CONSTANTS ------ *
ONE      DC    F'1'
DECVALUE DS    CL8
EDPAT    DC    X'402020206B2020206B202120'
         DC    C' '
BLANKS   DC    CL133' '
OUTLINE  DS    CL133
HEADER   DC    CL133'1  CKIEBGEN - SEQUENTIAL COPY PROGRAM'
         DC    CL133' '
MSGDSN   DC    CL133'-     SYSUTX DSN ='
         DC    CL133' '
MSGMBR   DC    CL133'         MEMBER  ='
         DC    CL133' '
MSGDSORG DC    CL133'         DSORG   ='
         DC    CL133' '
MSGBLKSZ DC    CL133'         BLKSIZE ='
         DC    CL133' '
MSGLRECL DC    CL133'         LRECL   ='
         DC    CL133' '
MSGRECFM DC    CL133'         RECFM   ='
         DC    CL133' '
MSG01    DC    CL133'-    RECORDS  IN ='
         DC    CL133' '
MSG02    DC    CL133'     RECORDS OUT ='
         DC    CL133' '
INRCDS   DC    PL7'0'
OUTRCDS  DC    PL7'0'
         LTORG
SYSUT1   DCB   DDNAME=SYSUT1,MACRF=GM,EODAD=FIN,DSORG=PS,EXLST=JFCB1
SYSUT2   DCB   DDNAME=SYSUT2,MACRF=PM,DSORG=PS,EXLST=JFCB1
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,LRECL=133
JFCB1    DC    X'87'
         DC    AL3(INFMJFCB)
         IEFJFCBN LIST=YES
AREA     DS    CL32768
         END   CKIEBGEN
