*------------------------------------------------------------- *
* Program to copy a sequential file to another sequential file *
* using QSAM.  GET a record and write it to a buffer.  PUT the *
* record from the buffer to an output file.  Loop until end of *
* input.                                                       *
*                                                              *
* QSAM does the blocking.  This program is very general, and   *
* you have to specify many of the dataset characteristics in   *
* the JCL, unless the system has another way to find them out. *
* (OPEN processing helps you out here, if the dataset is       *
* cataloged.)                                                  *
*                                                              *
* Qriginal program was from Baldomero Castilla.                *
* (Adapted to comment in English, and to use only IBM macros)  *
*------------------------------------------------------------- *
CKIEBGEN CSECT
         YREGS
         USING *,R15
         B     EYECATCH
         DC    C'CKIEBGEN'
SAVEAREA DC    18F'0'
EYECATCH DS    0H             ENSURE ALIGNMENT
         SAVE  (14,12)
         LR    R5,R1          SAVE PARM POINTER
         LA    R1,SAVEAREA          NEW SAVE-AREA ADDR
         ST    R1,8(,R13)           FORWARD SAVE-AREA CHAIN PTR
         ST    R13,4(,R1)           BACKWARD SAVE-AREA CHAIN PTR
         LR    R13,R1               NEW SAVE AREA ADDRESS
         LR    R1,R5          RESTORE PARM POINTER
         DROP  R15
* -----                       SET UP ONE BASE REGISTER
         USING CKIEBGEN,R12
         LR    R12,R15
* -----                       OPEN INPUT AND OUTPUT FILES
         OPEN  (SYSUT1,(INPUT))
         OPEN  (SYSUT2,(OUTPUT))
* -----                       READ-WRITE AND LOOP UNTIL END
LOOPIT   GET   SYSUT1,AREA
         PUT   SYSUT2,AREA
         B     LOOPIT
* -----                       AT END OF INPUT, CLOSE FILES
FIN      CLOSE SYSUT1
         CLOSE SYSUT2
* -----                       AND FINISH
THEEND   DS    0H
         L     R13,4(,R13)
         RETURN (14,12),RC=0
* -----                       INPUT AND OUTPUT QSAM DCB'S
SYSUT1   DCB   DDNAME=SYSUT1,MACRF=GM,EODAD=FIN,DSORG=PS
SYSUT2   DCB   DDNAME=SYSUT2,MACRF=PM,DSORG=PS
* -----
AREA     DS    CL32768        THIS IS THE DATA BUFFER
         END   CKIEBGEN
